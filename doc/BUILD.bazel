load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@io_bazel_stardoc//stardoc:stardoc.bzl", "stardoc")

_DOC_SRCS = [
    "apple",
    "dtrace",
    "ios.doc",
    "macos.doc",
    "resources",
    "tvos.doc",
    "versioning",
    "watchos.doc",
]

[
    stardoc(
        name = file + "_doc",
        out = file + ".md",
        input = "//apple:%s.bzl" % file,
        tags = ["no-sandbox"],  # https://github.com/bazelbuild/stardoc/issues/112
        deps = ["//apple:" + file],
    )
    for file in _DOC_SRCS
]

# Help developers who get a red CI result by telling them how to fix it
_failure_message = "\nPlease update the docs by running\n    bazel run //doc:update"

[
    diff_test(
        name = "check_" + file,
        failure_message = _failure_message,
        file1 = file + ".md",
        file2 = "rules-%s.md" % file.replace(".doc", ""),
    )
    for file in _DOC_SRCS
]

# Special case since providers.md in the source folder doesn't follow the naming convention
stardoc(
    name = "providers_doc",
    out = "providers.gen.md",
    input = "//apple:providers.bzl",
    deps = ["//apple:providers"],
)

diff_test(
    name = "check_providers",
    failure_message = _failure_message,
    file1 = ":providers_doc",
    file2 = "providers.md",
)

write_file(
    name = "gen_update",
    out = "update.sh",
    content = [
        "#!/usr/bin/env bash",
        "cd $BUILD_WORKSPACE_DIRECTORY",
        "cp -fv bazel-bin/doc/providers.gen.md doc/providers.md",
    ] + [
        "cp -fv bazel-bin/doc/{src}.md doc/rules-{dst}.md".format(
            src = file,
            dst = file.replace(".doc", ""),
        )
        for file in _DOC_SRCS
    ],
)

sh_binary(
    name = "update",
    srcs = ["update.sh"],
    data = [file + ".md" for file in _DOC_SRCS],
)
